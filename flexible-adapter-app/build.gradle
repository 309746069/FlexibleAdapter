apply plugin: 'com.android.application'
apply plugin: 'eu.davidea.grabver'

versioning {
    major 5
    minor 0
    preRelease "SNAPSHOT"
}

android {
    compileSdkVersion targetSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId 'eu.davidea.samples.flexibleadapter'
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versioning.code
        versionName versioning.name

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def fileName = 'flexible-adapter-v' + versioning.name + '-c' + versioning.code + '.apk'
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix ' #' + versioning.build + versioning.builtOn
            minifyEnabled false
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // FlexibleAdapter + extensions
    compile project(':flexible-adapter')
    compile project(':flexible-adapter-ui')
    compile project(':flexible-adapter-databinding')

    // Support Libraries
    compile supportLib.appcompat
    compile supportLib.cardView
    compile(supportLib.design) {
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }
    compile(supportLib.recyclerView) {
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }

    // Timber (Logger) - https://github.com/JakeWharton/timber
    compile libraries.timber

    // Butter Knife (View Injector) - https://github.com/JakeWharton/butterknife
    compile libraries.butterKnife
    annotationProcessor libraries.butterKnifeCompiler

    // Glide (Image Loader & Caching)
    compile libraries.glide
    annotationProcessor libraries.glideCompiler

    // Smooth-app-bar-layout
    compile('me.henrytao:smooth-app-bar-layout:24.1.1.0') {
        exclude group: 'com.android.support'
    }

    // UI Widgets
    compile widgets.flipView

    // Test libraries
    androidTestCompile supportLib.annotations
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    testCompile 'org.robolectric:robolectric:3.4.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'

}